%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                               %
% FTA
%                                                               %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


ARCHI_TYPE fta(
		const integer wsn_num := 10,
		const integer app_num := 10,
		const rate wsn_net := 20000,
        const rate analyze_rate := 100000,
        const rate refine_rate := 200000,
		const rate deliver_sensed_data_rate := 80000,
        const rate broadcast_rate := 80000,
		const rate store_db := 100000,
        const weight alarm_prob := 0.1
		)

ARCHI_ELEM_TYPES

	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  WSN
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	WSN_Type(const rate wsn_net)
		BEHAVIOR
			WSN(void; void) = 
				<generate_sensed_data, inf> . <transmit_sensed_data, exp(wsn_net)> . WSN()

		INPUT_INTERACTIONS
			UNI generate_sensed_data
	
		OUTPUT_INTERACTIONS 
			UNI  transmit_sensed_data


	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  Gateway
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	Gateway_Receiver_Type(const rate deliver_sensed_data_rate)
		BEHAVIOR 	
			Gateway(void; void) =
					<receive_sensed_data, _> . <deliver_sensed_data, exp(deliver_sensed_data_rate)>  . Gateway()

		INPUT_INTERACTIONS
			OR receive_sensed_data


		OUTPUT_INTERACTIONS 
			UNI deliver_sensed_data

	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  Gateway
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	Gateway_Sender_Type(const rate broadcast_rate)
		BEHAVIOR 	
			Gateway(void; void) =
					<receive_refined_data, _> . <deliver_refined_data, exp(broadcast_rate)>  . Gateway()
					

		INPUT_INTERACTIONS
			UNI receive_refined_data


		OUTPUT_INTERACTIONS 
			AND deliver_refined_data


	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  Core
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	Core_Type(const weight alarm_prob,const rate analyze_rate, const rate refine_rate)
		BEHAVIOR 	
			Core(void; void) =
				<receive_sensed_data, _> . 
				choice{
					<normal_status, inf(1, 1 - alarm_prob)>  . <analyze_sensed_data, exp(analyze_rate)> . <transmit_sensed_data, inf>. Core(),
					<alarm_status, inf(1, alarm_prob)>  . <refine_sensed_data, exp(refine_rate)> . <transmit_refined_data, inf>. Core()
				}

		INPUT_INTERACTIONS
			UNI receive_sensed_data
	
		OUTPUT_INTERACTIONS 
			UNI transmit_sensed_data;
			transmit_refined_data
			


	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  DB
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	Rel_DB_Type(const rate store_db)
		BEHAVIOR 	
			DB(void; void) =
					<receive_sensed_data, _>. <store, exp(store_db)>  . DB()

		INPUT_INTERACTIONS
			UNI receive_sensed_data
	
		OUTPUT_INTERACTIONS 
			void



	%%%%%%%%%%%%%%%%%%%%%%%%%%%%
	%  Mobile App
	%%%%%%%%%%%%%%%%%%%%%%%%%%%%

	ELEM_TYPE 	Mobile_App_Type(void)
		BEHAVIOR 	
			App(void; void) = 
					<receive_refined_data, _> . <consume_refined_data,inf> .App()

		INPUT_INTERACTIONS
			UNI receive_refined_data
	
		OUTPUT_INTERACTIONS 
			UNI consume_refined_data


ARCHI_TOPOLOGY
       ARCHI_ELEM_INSTANCES
		FOR_ALL i IN 1..wsn_num
			WSN[i] : WSN_Type(wsn_net);
		FOR_ALL i IN 1..app_num
			App[i] : Mobile_App_Type();
		GR : Gateway_Receiver_Type(deliver_sensed_data_rate);
		GS : Gateway_Sender_Type(broadcast_rate);
		Core: Core_Type(alarm_prob,analyze_rate,refine_rate);
		RDB : Rel_DB_Type(store_db)

       ARCHI_INTERACTIONS
		FOR_ALL i IN 1..wsn_num
            WSN[i].generate_sensed_data;
        FOR_ALL i IN 1..app_num
            App[i].consume_refined_data


       ARCHI_ATTACHMENTS
		FOR_ALL i IN 1..wsn_num
			FROM WSN[i].transmit_sensed_data TO  GR.receive_sensed_data;
		FROM GR.deliver_sensed_data TO Core.receive_sensed_data;
		FROM Core.transmit_sensed_data TO RDB.receive_sensed_data;	
		FROM Core.transmit_refined_data TO GS.receive_refined_data;
		FOR_ALL i IN 1..app_num
			FROM GS.deliver_refined_data TO  App[i].receive_refined_data

END

