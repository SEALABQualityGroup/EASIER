ARCHI_TYPE fta(	const rate msg_gen_rate := 150, %Number of messages sent from the WSN in one second
				const rate map_gen_rate := 7.5, %Number of messages sent from FTA Manager to Fire fighters application to display the maps
				const rate AR_gen_rate := 60, %Number of messages sent from FTA Manager to Fire fighters application to display the augmented realty
				const rate video_gen_rate := 60, %Number of messages sent from Camera Robot to FTA Manager
				const rate timeout_rate := 1,
				const rate prop_rate := 150, %Number of messages sent from the Base Station to WSN and to FTA Manager
				const boolean flag := false) 
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
% The upper constant formal paramiters comes from the execution graphs and queueing networks 															  %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

ARCHI_ELEM_TYPES

	ELEM_TYPE Sensors_Type ( const rate msg_gen_rate,
				const rate timeout_rate,
				const boolean flag)
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		% The element sensors represent the entire WSN and consist in 3 different states. The first one, that is the basic state, is the waiting represents
		% the situation in which the sensors have the common behavior and also waits for the agent in case of fire. If the received bit is false, we are
		% in a common situation and so, after the timeout, we can go to the sending status in which the data (msg0) are sent to the Base station and come 
		% back to the waiting state. If the received bit is true we are in alarm situation so we go to the alarming status where the data plus audio (msg1)  
		% are sent to the Base Station and then we come back to the waiting state.
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		BEHAVIOR
			waiting (boolean expected_bit := flag; local boolean received_bit) =
				<receive_bit?(received_bit), _>.
				choice
				{
					cond(received_bit = expected_bit) ->
						<timeout, exp(timeout_rate)>.sending(),
					cond(received_bit != expected_bit) ->
						<activate_audio, inf>.alarming()
				};
				
			sending(void; void) =
				<generate_msg0, exp(msg_gen_rate)>.<transmit_msg0!(flag), inf>.waiting(false);
				
			alarming(void; void) =
				<generate_msg1, exp(msg_gen_rate)>.<transmit_msg1!(flag), inf>.waiting(false)
				
		INPUT_INTERACTIONS
			UNI receive_bit;
				generate_msg0;
			    generate_msg1

		OUTPUT_INTERACTIONS
			UNI transmit_msg0;
				transmit_msg1
			
ELEM_TYPE BaseStation_Type ( const rate prop_rate,
				const boolean flag)
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		% The element BaseStation has the only function to allow the communication between the WSN and the FTA Manager. The basic behavior is the wait in
		% which if the variable received agent is true it denote that we are in alarm situation so the agent received must be sent to the WSN (transmit_agent).
		% If the received bit (0) is false it denote that we are in a common situation so a normal message is received from the WSN and must be sent to the
		% FTA Manager. If the received bit (1) is false it denote that we are in a alarm situation so an augmented message is received from the WSN and must be 
		% sent to the FTA Manager.
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		BEHAVIOR
			wait (boolean expected_bit := flag; local boolean received_bit0, local boolean received_bit1,local boolean received_agent) =
				<receive_bit0?(received_bit0), _>.<receive_bit1?(received_bit1), _>.<receive_agent?(received_agent), _>.
				choice
				{
					cond(received_agent != flag) ->
						<propagate_agent, exp(prop_rate)>.<transmit_agent!(!flag), inf>.wait(expected_bit),
					cond(received_bit0 = expected_bit) ->
						<propagate_data, exp(prop_rate)>.<transmit_data0!(flag), inf>.wait(expected_bit),
					cond(received_bit0 != expected_bit) ->
						stop,
					cond(received_bit1 = expected_bit) ->
						<propagate_data, exp(prop_rate)>.<transmit_data1!(flag), inf>.wait(expected_bit),
					cond(received_bit1 != expected_bit) ->
						stop
				}
			
			INPUT_INTERACTIONS
				UNI propagate_agent;
					propagate_data;
					receive_bit0;
					receive_bit1;
					receive_agent
				
			OUTPUT_INTERACTIONS
				UNI transmit_data0;
					transmit_data1;
					transmit_agent
			
	ELEM_TYPE FtaManager_Type (const rate map_gen_rate,
				const rate AR_gen_rate, const boolean flag)	
		BEHAVIOR
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		% The basic behavior of the Fta Manager element is the receive. We have to choice: if we receive a normal messages from the Base Station (data0) we
		% analyze them and in case of normal temperature the FTA Manager store the data and comes back in receive state instead if the temperature is too high
		% the FTA Manager store the data, send the alarm to fire fighters station, turn on the robot, generate map and send the map to the fire fighters
		% application then we go in the alarm status in which the video received from the robot is augmented with the collected data and sent to the fire
		% fighters application; if we receive an augmented messages (data1) we already are in an alarm state so simply store the data and go to the alarm
		% status for the augemented realty.
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
			receive (boolean expected_bit := flag; local boolean received_data0, local boolean received_data1) =
				<receive_data0?(received_data0), _>.<receive_data1?(received_data1), _>.
				choice
				{
					cond(received_data0 = expected_bit) -> %normal message received
						<analyze_data, inf>.
						choice
						{
							<tempnormal, inf>.<store_data, inf>.receive(flag),
							<tempsup50, inf>.<store_data, inf>.<fire_alert!(!flag), inf>.<send_agent!(!flag), inf>.<robot_on!(!flag), inf>.
											<generate_map, exp(map_gen_rate)>.<transmit_map!(!flag), inf>.alarm(!flag)
						},
					cond(received_data1 = expected_bit) -> %augmented message received
						<store_data, inf>.alarm(!flag)
				};
		
			alarm (boolean expected_bit; local boolean received_video) =	
				<receive_video?(received_video), _>.
				choice
				{
					cond(received_video = expected_bit) -> %robot on
						<generate_AR, exp(AR_gen_rate)>.<transmit_AR!(!flag), inf>.receive(!flag),
					cond(received_video != expected_bit) -> %robot off
						stop
				}
		
		INPUT_INTERACTIONS
				UNI receive_data0;
					receive_data1;
					receive_video;
					generate_map;
					generate_AR
				
			OUTPUT_INTERACTIONS
				UNI fire_alert;
					send_agent;
					robot_on;
					transmit_map;
					transmit_AR

	ELEM_TYPE CameraRobot_Type (const rate video_gen_rate, const boolean flag)	
		BEHAVIOR
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		% The basic behavior of the Camera Robot element is the waiting. If the received bit (robot) is false the alarm ended and so the robot is turn off.
		% Instead if the received bit is true we are in an alarm situation and so we turn on the robot and go to the transmit state in which the video is
		% created and sent to the FTA Manager. Then we come back to the waiting state.
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
			waiting(boolean expected_bit := flag; local boolean robot) =
				<receive_com?(robot), _>.
				choice
				{
					cond(robot = expected_bit) -> %no alarm
						<set_off, inf>.waiting(flag),
					cond(robot != flag) -> %alarm
						<set_on, inf>.transmit()
				};
				
			transmit(void;void) =
				<generate_video, exp(video_gen_rate)>.<transmit_video!(!flag), inf>.waiting(flag)
				
		INPUT_INTERACTIONS
			UNI receive_com;
				generate_video
				
		OUTPUT_INTERACTIONS
			UNI transmit_video

	ELEM_TYPE Application_Type (const boolean flag)	
		BEHAVIOR
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		% The basic behavior of the Application element is the waiting one. If the received bit (alarm) is set to false the element is hidle. If the received
		% bit is true we are in alarm status and so the Application receive the map and the augmented realty from the FTA Manager.
		%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
			waiting(boolean expected_bit := flag; local boolean alarm, local boolean map, local boolean AR) =
				<receive_alarm?(alarm), _>.
				choice
				{
					cond(alarm = expected_bit) ->
						stop,
					cond(alarm != flag) ->
						<receive_map?(map), _>.<receive_AR?(AR), _>.waiting(flag)
				}
				
		INPUT_INTERACTIONS
			UNI receive_alarm;
				receive_map;
				receive_AR
				
		OUTPUT_INTERACTIONS
			void

ARCHI_TOPOLOGY
	ARCHI_ELEM_INSTANCES
		S: Sensors_Type (msg_gen_rate, timeout_rate, flag);
		BS: BaseStation_Type (prop_rate, flag);
		FTA: FtaManager_Type (map_gen_rate, AR_gen_rate, flag);
		CR: CameraRobot_Type (video_gen_rate, flag);
		FFA: Application_Type (flag)
		
ARCHI_INTERACTIONS
		S.generate_msg0;
		S.generate_msg1;
		BS.propagate_data;
		BS.propagate_agent;
		FTA.generate_map;
		FTA.generate_AR;
		CR.generate_video
		
ARCHI_ATTACHMENTS
		FROM S.transmit_msg0 TO BS.receive_bit0;
		FROM S.transmit_msg1 TO BS.receive_bit1;
		FROM BS.transmit_agent TO S.receive_bit;
		FROM FTA.send_agent TO BS.receive_agent;
		FROM BS.transmit_data0 TO FTA.receive_data0;
		FROM BS.transmit_data1 TO FTA.receive_data1;
		FROM CR.transmit_video TO FTA.receive_video;
		FROM FTA.robot_on TO CR.receive_com;
		FROM FTA.fire_alert TO FFA.receive_alarm;
		FROM FTA.transmit_map TO FFA.receive_map;
		FROM FTA.transmit_AR TO FFA.receive_AR
		
END
			