%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%                                                               %
% FTA System AEmilia model - UC EmergencyHandling
%                                                               %
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%


ARCHI_TYPE FTASystem_Type(const rate   msg_gen_rate  := 5,
		    const rate   forward_rate  := 5,
		    const rate   prop_rate     := 5,
		    const weight stop_prob := 0.2)


ARCHI_ELEM_TYPES

	% FTA_Type modella l'FTA Server
	ELEM_TYPE FTA_Type(const rate forward_rate)

	    BEHAVIOR
		
	      % descrive il processo di miglioramento di qualita' video proveniente dal camera robot
	      Forward(void; void) =
	      	<inFTA1, _> . <forward, exp(forward_rate)> . <outFTA1, inf> . Forward()

	    INPUT_INTERACTIONS

	      UNI inFTA1

	    OUTPUT_INTERACTIONS

	      UNI outFTA1


	% NetHi_Type modella la rete ad alta priorita'
	ELEM_TYPE NetHi_Type(const rate prop_rate)

	    BEHAVIOR

	      % modella l'instradamento dei dati nelle direzioni di upstream e downstream attraverso rete
	      Transmit(void; void) =
	      choice{
	      	<inNetH1, _> . <downstream, exp(prop_rate)> . <outNetH1, inf> . Transmit(),
	      	<inNetH2, _> . <upstream, exp(prop_rate)> . <outNetH2, inf> . Transmit()
	      }

	    INPUT_INTERACTIONS

	      UNI inNetH1;
		  inNetH2

	    OUTPUT_INTERACTIONS

	      UNI outNetH1;
		  outNetH2


	% MobApp_Type modella l'applicazione mobile che riceve il video elaborato dall'FTA server
	ELEM_TYPE MobApp_Type(void)

	    BEHAVIOR

	      % ottiene e visualizza il video in streaming
	      GetVideo(void; void) = 
	      	<inMA1, _> . <getVideo, inf> . GetVideo()

	    INPUT_INTERACTIONS

	      UNI inMA1

	    OUTPUT_INTERACTIONS

	      UNI getVideo
		  
	
	% Robot_Type modella il Camera Robot
	ELEM_TYPE Robot_Type(const rate msg_gen_rate, const weight stop_prob)

	    BEHAVIOR

	      % acquisisce il video per inviarlo in direzione dell'FTA server tramite la rete
	      SendVideo(void; void) = 
	      	<sendVideo, exp(msg_gen_rate)> . <outR1, inf(1, stop_prob)> . SendVideo()

	    INPUT_INTERACTIONS

	      UNI sendVideo

	    OUTPUT_INTERACTIONS

	      UNI outR1


ARCHI_TOPOLOGY


	% definisce le istanze 
	ARCHI_ELEM_INSTANCES

	  FTA  : FTA_Type(forward_rate);
	  NETH : NetHi_Type(prop_rate);
	  MA : MobApp_Type();
	  R  : Robot_Type(msg_gen_rate, stop_prob)


	ARCHI_INTERACTIONS

	  MA.getVideo;
	  R.sendVideo


	% modella i collegamenti tra le istanze
	ARCHI_ATTACHMENTS

	  FROM R.outR1     		TO NETH.inNetH1;
	  FROM NETH.outNetH1    TO FTA.inFTA1;
	  FROM FTA.outFTA1		TO NETH.inNetH2;
	  FROM NETH.outNetH2	TO MA.inMA1


END
